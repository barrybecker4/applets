// Copyright by Barry G. Becker, 2013. Licensed under MIT License: http://www.opensource.org/licenses/MIT
// This gradle build file will build all applet subprojects.
// @author Barry Becker

description = 'Applets'
defaultTasks 'build'

subprojects {
    apply plugin: 'java'
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
    apply plugin: 'project-report'
    apply plugin: 'application'
    apply plugin: 'idea'

    defaultTasks 'build'
    version = '1.0'

    repositories {
        mavenCentral()
        maven {
            url "http://repo1.maven.org/maven2/"
            url "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases"
        }
        flatDir {
            name "fileRepo"
            dirs "../repository"
        }
    }

    dependencies {
        compile 'commons-collections:commons-collections:3.2'
        testCompile 'junit:junit:3.+'
    }

    jar {
         manifest {
            attributes 'Implementation-Title': 'Applets', provider: 'gradle', 'Implementation-Version': version
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        //baseName = 'bb4-common'
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        //baseName = 'bb4-common'
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    javadoc.doLast {
        copy {
            from sourceSets.main.allSource
            into javadoc.destinationDir
            include '**/package.png'
        }
    }

    clean.doLast {
        // project. is needed to disambiguate from the clean tasks existing delete.
        project.delete(project.repositories.fileRepo.dirs)
    }


    task copyToLib(dependsOn: jar, type: Copy) {
        description = "Copies the downloaded dependencies to the lib directory so they can be deployed (see webdeployment)"
        into "build/libs"
        from configurations.runtime
    }

    uploadArchives {
        repositories {
            add project.repositories.fileRepo
        }
    }

    // all subprojects use this directory layout.
    sourceSets {
        main {
            java {
                srcDir 'source'
            }
            resources {
                srcDir 'source'
            }
        }
        test {
            java {
                srcDir 'test'
            }
            resources {
                srcDir 'test'
            }
        }
    }
}


