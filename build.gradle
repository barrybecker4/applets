// Copyright by Barry G. Becker, 2013. Licensed under MIT License: http://www.opensource.org/licenses/MIT
// This gradle build file will build all applet subprojects.
// @author Barry Becker
apply plugin: 'idea'

description = 'Applets'
defaultTasks 'build'
ext.depVersion = getDependentVersion()

subprojects {
    apply plugin: 'java'
    apply plugin: 'scala'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    apply plugin: 'project-report'
    apply plugin: 'application'

    defaultTasks 'build'
    version = '1.1'

    repositories {
        mavenCentral()
        maven { url "http://repo1.maven.org/maven2/"  }
        // this Sonatype repo is where released bb4 jars come from
        maven { url "https://oss.sonatype.org/content/repositories/releases" }
        // this Sonatype repo is where SNAPSHOT bb4 jars come from
        maven { url "https://oss.sonatype.org/content/groups/staging" }
        //maven { url "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases" }
    }

    // all subprojects use this directory layout.
    sourceSets {
        main {
            java {
                srcDir 'source'
            }
            resources {
                srcDir 'source'
            }
        }
        test {
            java {
                srcDir 'test'
            }
            resources {
                srcDir 'test'
            }
        }
    }
}

// Gradle 3.x did not find message bundles in buildSrc for some reason.
wrapper {
    gradleVersion = '4.10.2'
}

/** read the version from message bundle. It is the version of all the bb4 jars */
String getDependentVersion() {
    def labels = ResourceBundle.getBundle('html', Locale.ENGLISH)
    println("version == " + labels.getString("version"))
    return labels.getString("version")
}
