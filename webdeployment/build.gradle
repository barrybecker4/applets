// Copyright by Barry G. Becker, 2013. Licensed under MIT License: http://www.opensource.org/licenses/MIT

import org.apache.tools.ant.filters.ReplaceTokens
version = '1.0'

ext.distributionGameDir="$distributionDir/applets"
ext.distributionSchemaDir="$distributionDir/schema"

// Location of all the project related files.
// The environment variable PROJECT_HOME needs to be set to something like:
//    E:/projects/java_projects/trunk   (on Windows)   or
//    /windows/projects/java_projects/trunk (on Linux)
ext.project_home="$System.env.PROJECT_HOME"

// Location of java SDK
ext.java_home="$System.env.JAVA_HOME"


dependencies {
    compile project(':optimization')
    compile project(':sound')
    compile project(':imageproc')
    compile project(':puzzle')
    compile project(':game')
    compile project(':apps')
}

clean.doLast {
    // project. is needed to disambiguate from the clean tasks existing delete.
    project.delete ("$distributionDir", "$stagingDir")
}

// Should be able to have a type: CopyTask and use << instead of doLast
task deployImages(description: "Copy image files to $distributionGameDir")  {
    doLast {
        copy {
            from "$sourceDir/images"
            into "$distributionGameDir"
            include '**/*.jpg'
            include '**/*.png'
            include '**/*.gif'
        }
    }
}

// Should be able to have a type: CopyTask and use << instead of doLast
task deploySchemas(description: "Copy schema files to $distributionDir") {
     doLast {
         copy {
             from fileTree("$project_home").files
             into "$distributionSchemaDir"
             include "**/*.dtd"
             include "**/*.xsd"
         }
     }
}

task deployJars(description: "Deploys jar files needed for running applets and web start applications") {
    doLast {
        println "deployJars to $distributionGameDir from $project_home"
        copy {
            from fileTree("$project_home").files
            into "$distributionGameDir"
            include "bb4-*.jar"
            include "jhlabs-1.0.3.jar"
            include "jigo-sgf-1.1.jar"
        }
    }
}

task deploy(dependsOn: [compileJava, deployImages, deploySchemas, deployJars],
            description: "Deploys all files  to $distributionGameDir so they can be bulk copied to a website") << {
    apply from: 'appletHtmlCommon.gradle'
    //println "appletIndexFile: " + appletIndexFile

    // copy all the js, css, image files (should only be done once, not once per locale)
    copy {
        from "$sourceDir/html"
        into "$distributionGameDir/.."
        include '*.js'
        include '*.css'
        include '*.gif'
    }

    /* there needs to be a separate set of html and jnlp files for each locale */
    println "localesList="+ appletHtml.localesList
    appletHtml.localesList.each {
        locale -> createHtmlForLocale(locale)
    }
}

/**
 * Deploys all the html files for the given locale.
 * This includes the index page, javascript, and individual applet/jnlp files.
 */
void createHtmlForLocale(Locale locale) {

    def labels = ResourceBundle.getBundle('html', locale)

    // make these replacements when copying
    def indexReplacements = getIndexReplacements(labels);
    def entryReplacements = getEntryReplacements(labels);
    def entryKeyReplacements = getEntryKeyReplacements(labels);

    copy {
        from "$sourceDir/html/" + appletIndexFile['base']
        into "$distributionGameDir/.."
        rename { String fileName ->
            fileName.replace('.html', '_' + locale.toString() + '.html')
        }
        filter(ReplaceTokens, tokens: indexReplacements, beginToken: '{', endToken: '}')
        filter(ReplaceTokens, tokens: entryReplacements, beginToken: '{', endToken: '}')
        filter(ReplaceTokens, tokens: entryKeyReplacements, beginToken: '{', endToken: '}')
    }


    appletHtml.get("appletList").each { applet ->
        def htmlReplacements = getHtmlReplacements(labels, applet)
        copy {
            from "$sourceDir/html/applet_template.html"
            into "$distributionGameDir"
            rename { String fileName ->
                fileName.replace('applet_template', applet + '_' + locale.toString())
            }
            filter(ReplaceTokens, tokens: htmlReplacements, beginToken: '{', endToken: '}')
        }
        def jnlpReplacements = getJnlpReplacements(labels, applet)
        copy {
            from "$sourceDir/html/applet_template.jnlp"
            into "$distributionGameDir"
            rename { String fileName ->
                fileName.replace('applet_template', applet + '_' + locale.toString())
            }
            filter(ReplaceTokens, tokens: jnlpReplacements, beginToken: '{', endToken: '}')
        }
    }
}

Map getIndexReplacements(ResourceBundle labels) {
    def replacements = new HashMap()
    htmlIndex.get("replacementList").each {
        key -> if (labels.containsKey(key)) {
            replacements.put(key, labels.getString(key))
        }
    }
    return replacements
}

/**
 * note: should use i18n replacements here. e.g   "a {0} b {1} c" and have only one TRTemplate in messages.
 * @return replacements for entries within the index page
 */
Map getEntryReplacements(ResourceBundle labels) {
    def TRTemplates = [labels.getString("TRTemplate1"), labels.getString("TRTemplate2"),
            labels.getString("TRTemplate3"), labels.getString("TRTemplate4"), labels.getString("TRTemplate5"),
            labels.getString("TRTemplate6"), labels.getString("TRTemplate7"),
            labels.getString("TRTemplate7a"), labels.getString("TRTemplate7b"),
            labels.getString("TRTemplate8"), labels.getString("TRTemplate9"), labels.getString("TRTemplate10")]
    def replacements = new HashMap()

    appletHtml.get("appletList").each {
        key ->
            def appKey = key + '.name'
            if (labels.containsKey(appKey)) {
                def appName = labels.getString(appKey)
                def titleKey = key + '.title'
                def loc = labels.getLocale().toString()
                replacements.put(key + "TR",
                        TRTemplates[0] + titleKey
                      + TRTemplates[1] + key + '.longDescription'
                      + TRTemplates[2] + appName + TRTemplates[3] + loc
                      + TRTemplates[4] + appName + TRTemplates[5] + loc
                      + TRTemplates[6] + appName + TRTemplates[7] + loc
                      + TRTemplates[8] + appName + TRTemplates[9] + titleKey
                      + TRTemplates[10] + titleKey + TRTemplates[11])
        }
    }
    return replacements
}

Map getEntryKeyReplacements(ResourceBundle labels) {
    def replacements = new HashMap()
    appletHtml.get("appletList").each { applet ->
        htmlIndex.get("entryKeyReplacementList").each { key ->
            def appletKey = applet + '.' + key;
            replacements.put(appletKey, labels.getString(appletKey))
        }
    }
    return replacements
}

Map getHtmlReplacements(ResourceBundle labels, String applet) {
    return getFileReplacements(labels, applet, html)
}

Map getJnlpReplacements(ResourceBundle labels, String applet) {
    return getFileReplacements(labels, applet, jnlp)
}

Map getFileReplacements(ResourceBundle labels, String applet, Map fileType) {
    def replacements = new HashMap()

    fileType.get("replacementList").each { key ->
        def appletKey = applet + '.' + key;
        if (labels.containsKey(appletKey))
            replacements.put(appletKey, labels.getString(appletKey))
        else
            replacements.put(appletKey, labels.getString(key))
    }
    //println replacements
    return replacements
}


